module Test_GenericType_mod
!@sum Tests GenericType implementation.
!@auth T. Clune
  use pFUnit_mod
  use GenericType_mod
  implicit none

  integer, parameter :: dp = selected_real_kind(14)

contains

@Test
  subroutine testGetInteger()
!@sum Test that GenericType can both be assigned to an integer,
!@+ and also can be compared against an integer.
    integer :: found

    found = GenericType(1)
    @assertEqual(1, found)

    @assertTrue(1 == GenericType(1))
    @assertTrue(2 == GenericType(2))
    @assertFalse(1 == GenericType(2))
  end subroutine testGetInteger

@Test
  subroutine testGetIntegerArray()
!@sum Test that GenericType can both be assigned to an array of integers,
!@+ and also can be compared against an array of integers.
    integer, parameter :: NUM_VALUES=2
    integer :: expected(NUM_VALUES), found(NUM_VALUES)

    expected = [1,2]
    found = GenericType(expected)
    @assertEqual(expected, found)
    @assertTrue(all(expected == GenericType(expected)))

  end subroutine testGetIntegerArray

@Test
  subroutine testGetReal64()
!@sum Test that GenericType can both be assigned to a double,
!@+ and also can be compared against a double.
    real(kind=dp) :: expected, found

    expected = 1.234d+0
    found = GenericType(expected)
    @assertEqual(expected, found)
    @assertTrue(expected == GenericType(expected))
    @assertFalse(expected-1 == GenericType(expected))

  end subroutine testGetReal64

@Test
  subroutine testToStringInteger()
!@sum Test that GenericType can be converted to a string.
    @assertEqual('1', toString(GenericType(1)))
    @assertEqual('2', toString(GenericType(2)))
  end subroutine testToStringInteger

@Test
  subroutine testToStringReal64()
!@sum Test that GenericType can be converted to a string.
!@+ Need to ensure that value -> string -> value reproduces
!@+ exactly.
    character(len=MAX_LEN_VALUE) :: string
    real(kind=dp) :: expectedValue, foundValue

    expectedValue = 321.1234567890123456789d+0
    string = toString(GenericType(expectedValue))
    read(string,'(g20.0)') foundValue
    @assertEqual(expectedValue, foundValue)

  end subroutine testToStringReal64

@Test
  subroutine testToStringLogical()
!@sum Test that GenericType can be converted to a string.
    @assertEqual('.true.', toString(GenericType(.true.)))
    @assertEqual('.false.', toString(GenericType(.false.)))
  end subroutine testToStringLogical

  subroutine testFromStringInteger()
!@sum Test that GenericType can be constructed from a string.
    @assertTrue(1 == GenericType('1', INTEGER_TYPE))
    @assertTrue(1234 == GenericType(' 1234', INTEGER_TYPE))
  end subroutine testFromStringInteger
    
@Test
  subroutine testFromStringReal64()
!@sum Test that GenericType can be constructed from a string.
    real(kind=dp) :: expected
    type (GenericType_type) :: generic

    expected = 1.23d+0
    generic = GenericType(expected)
    @assertTrue(expected == GenericType(toString(generic), REAL64_TYPE))

    expected = -123.789d+0
    generic = GenericType(expected)
    @assertTrue(expected == GenericType(toString(generic), REAL64_TYPE))

  end subroutine testFromStringReal64

@Test
  subroutine testFromStringLogical()
!@sum Test that GenericType can be constructed from a string.
    @assertTrue(.false. == GenericType('f', LOGICAL_TYPE))
    @assertTrue(.false. == GenericType('false', LOGICAL_TYPE))
    @assertTrue(.false. == GenericType('.False.', LOGICAL_TYPE))

    @assertTrue(.true. == GenericType('T', LOGICAL_TYPE))
    @assertTrue(.true. == GenericType('true', LOGICAL_TYPE))
    @assertTrue(.true. == GenericType('.True.', LOGICAL_TYPE))
  end subroutine testFromStringLogical
    
! TODO: This test needs to be revisited
@Test
  subroutine testFailFromStringInteger()
!@sum Test that GenericType() returs appropriate errors when args are
!@+ inconsistent.
    integer :: i

#ifdef USE_PFUNIT
    i = GenericType('1.234', INTEGER_TYPE)
    @assertExceptionRaised('GenericType::GenericType() - cannot convert string "1.234" to integer.')
    i = GenericType('1.234', -1)
    @assertExceptionRaised('GenericType::GenericType() - no such type.')
#endif

  end subroutine testFailFromStringInteger
    
! TODO: This test needs to be revisited
@Test
  subroutine testFailFromStringLogical()
!@sum Test that GenericType() returs appropriate errors when args are
!@+ inconsistent.
    logical :: flag

#ifdef USE_PFUNIT
    flag = GenericType('fa', LOGICAL_TYPE)
    @assertExceptionRaised('GenericType::GenericType() - cannot convert string "fa" to logical.')
#endif

  end subroutine testFailFromStringLogical
    
end module Test_GenericType_mod
